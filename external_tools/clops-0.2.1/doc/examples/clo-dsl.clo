
NAME::
 CLODSL: 
 "CLOPS is a tool which helps programmers to develop command-line parsers and 
  option documentation for their programs. 
  Given a description of command-line options in the CLOPS language, 
  it generates a corresponding parser and documentation. 
  The format of the documentation is determined by Velocity templates."

ARGS::

Output:{"-o","--output"}:{file}:[mustExist,mustbedir,default="."]
  :"Output directory for generated Java files."
Test:{"-m","--main"}
  :"Generate a Main class with a main method for rapid testing/debugging."
 
  
//Strip quotes so we can write -p="" or -p "" for default package.
OutputPackage:{"-p","--package"}:{string}:[stripquotesifpresent,blankparamallowed]
  :"Output Java package. If left empty, the default package is used."
  
Docs:{"-d","--docs"}
  :"Use a default documentation template for generation."

Builtin:{"-b","--built-in"}:{enum-list}:[choices="htmldev,html,manpage,usage,help"]
  :"Use a specific built-in documentation template for generation (you must specify one of the following: htmldev,html,manpage,usage)."

Custom:{"-c","--custom"}:{file-list}:[mustExist,canBeDir="false"]
  :"Use custom templates for generation."

Target:{"-t", "--target"}:{file}
  :"Specify the target directory / or the target file for the generation from some templates."
  
Verbose:{"-v","--verbose"}:[default="false"]
  :"Print debugging messages."

Version:{"-version"}
  :"Print version information and exit"

//option_factory should probably be a regular expression (for qualified java class names).
OptionFactory:{"-of","--option-factory"}:{string}
  :"Use this option factory instead of the default. Must be a fully qualified class name." 

TransitiveFlyRules:{"-tfr","--transitive-fly-rules"}:[default="false"]
  :"Fly rules in the generated parser will be applied
  transitively. Meaning that assigning to an option in a fly-rule
  triggers fly-rules associated with that opion. This is an advanced
  and experimental feature. The issue with is it that the parser
  becomes potentially non-terminating due to rules triggering one
  another."

Input:{}:{file}:[between="",mustExist,canbedir="false"]
  :"Input CLOPS file."

FORMAT::
    (All* Input All*) | Version
  : "The program takes the input file surrounded by options.";

WHERE::

All:  AllOptions -Input;
Base: Output | OutputPackage | Test | Verbose;
Templates: Docs | Builtin | Custom | Target;
Advanced: OptionFactory | TransitiveFlyRules;


FLY::

OVERRIDES::

VALIDITY::

{ !$(Output?) } : "The output should always be set to a valid value";

//exclusive : gen_docs_builtin, gen_docs_custom   
//          : "Only one type of documentation template may be used." 
//          ;

//requires : gen_docs => gen_docs_builtin || gen_docs_custom 
//         : "You must specify the documentation type to generate."
//         ;
         
//requires : gen_docs_builtin => gen_docs
//         : "--built-in cannot be used without --docs"
//         ;
         
//requires : gen_docs_custom => gen_docs
//         : "--custom cannot be used without --docs"
//         ;
