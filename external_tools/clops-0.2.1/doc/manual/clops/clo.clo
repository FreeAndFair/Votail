
NAME::
 CLODSL

PACKAGE::
 ie.ucd.clops.dsl.generatedinterface

ARGS::

output:{"-o","--output"}:{file}:[mustExist,mustbedir,default="."]
  :"Output directory for generated files."
//Strip quotes so we can write -p="" or -p "" for default package.
output_package:{"-p","--package"}:{string}:[stripquotesifpresent]
  :"Output package. If left empty the default package is used."
gen_test:{"-t","--test"}
  :"Generate a Main class with a main method for rapid testing/debugging."

//option_factory should probably be a regular expression (for qualified java class names).
option_factory:{"-of","--option-factory"}:{string}
  :"Use this option factory instead of the default. Must be a fully qualified class name."

gen_docs:{"-d","--docs"}:{file}:[canBeDir="false"]
  :"Generate documentation and write it to the given output file."
gen_docs_builtin:{"-b","--built-in"}:{string-enum}:[choices="html,txt"]
  :"Use a built-in template for documentation generation."
gen_docs_custom:{"-c","--custom"}:{file}:[mustExist,canBeDir="false"]
  :"Use a custom template for documentation generation."
  
verbose:{"-v","--verbose"}:[default="false"]
  :"Print debugging messages."

 
transitiveFlyRules:{"-tfr","--transitive-fly-rules"}:[default="false"]
  :"Fly rules will applied transtiviely."

input:{}:{file}:[between="",mustExist,canbedir="false"]
  :"Input file."

FORMAT::

//Currently enforcing presence of these options through this format.
//When we have validity up and running this will not be necessary
  all_args* input all_args*
;

WHERE::

all_args:    output 
          OR output_package 
          OR option_factory 
          OR gen_test 
          OR gen_docs
          OR gen_docs_builtin
          OR gen_docs_custom
          OR verbose
          OR transitiveFlyRules
         ;

FLY::

OVERRIDES::

VALIDITY::

{ !$(output?) } : "output not set";

exclusive : gen_docs_builtin, gen_docs_custom   
          : "Only one type of documentation template may be used." 
          ;

requires : gen_docs => gen_docs_builtin || gen_docs_custom 
         : "You must specify the documentation type to generate."
         ;
         
requires : gen_docs_builtin => gen_docs
         : "--built-in cannot be used without --docs"
         ;
         
requires : gen_docs_custom => gen_docs
         : "--custom cannot be used without --docs"
         ;